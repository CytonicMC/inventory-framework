import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'maven-publish'
}

group 'me.saiintbrisson.minecraft'
version '2.4.0'

repositories {
    mavenCentral()
    mavenLocal()
    maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url = 'https://oss.sonatype.org/content/repositories/snapshots' }
}

dependencies {
    compileOnly libs.spigot.api
    compileOnly libs.jetbrains.annotations
    testImplementation libs.junit.api
    testRuntimeOnly libs.junit.engine
    testImplementation libs.spigot.api
    testCompileOnly libs.jetbrains.annotations
}

test {
    useJUnitPlatform()
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        duplicatesStrategy DuplicatesStrategy.INCLUDE
        filter ReplaceTokens, tokens: [version: version]
    }
}

java {
    withSourcesJar()
    withJavadocJar()
}

javadoc {
    exclude 'me/matsubara/**'
    exclude 'me/saiintbrisson/minecraft/utils/**'
}

project.afterEvaluate {
    publishing {
        publications {
            jitpack(MavenPublication) {
                groupId = 'com.github.DevNatan'
                artifactId = 'inventory-framework'

                from components.java
            }
        }
    }
}