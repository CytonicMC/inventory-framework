plugins {
    id 'maven-publish'
}

project.afterEvaluate {
    publishing {
        repositories {
            maven {
                url = uri("https://maven.pkg.github.com/DevNatan/inventory-framework")
                credentials {
                    username = System.getenv("USERNAME")
                    password = System.getenv("TOKEN")
                }
            }
        }

        publications {
            jitpack(MavenPublication) {
                configurePublication(it)
            }

            register("gpr", MavenPublication) {
                configurePublication(it)
            }
        }
    }
}

tasks.withType(GenerateModuleMetadata).configureEach {
    enabled = false
}

@SuppressWarnings('unused')
def configurePublication(Publication publication) {
    publication.groupId = 'com.github.DevNatan'
    publication.artifactId = 'inventory-framework'

    publication.from components.java

    publication.pom {
        url.set('https://gihub.com/DevNatan/inventory-framework.git')
        licenses {
            license {
                name = 'MIT license'
                url = 'https://github.com/DevNatan/inventory-framework/blob/main/LICENSE'
            }
        }

        scm {
            connection = 'scm:git:git://github.com/DevNatan/inventory-framework.git'
            developerConnection = 'scm:git:ssh://github.com/DevNatan/inventory-framework.git'
            url = 'https://github.com/DevNatan/inventory-framework'
        }
    }
}