apply plugin: 'org.danilopianini.publish-on-central'

ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

publishOnCentral {
    it.configureMavenCentral = true
    it.projectDescription = 'Inventory Framework is a Bukkit Inventory API framework that allows you to create custom inventories with varied interaction treatments.'
    it.projectLongName = project.name
    it.licenseName = 'MIT license'
    it.licenseUrl = 'https://github.com/DevNatan/inventory-framework/blob/main/LICENSE'
    it.repoOwner = 'DevNatan'
    it.projectUrl = 'https://github.com/DevNatan/inventory-framework'
    it.scmConnection = 'scm:git:https://github.com/DevNatan/inventory-framework.git'

    it.mavenCentral.user.set(project.findProperty("ossrh.username") ?: System.getenv('OSSRH_USERNAME'))
    it.mavenCentral.password.set(provider { project.findProperty("ossrh.password") ?: System.getenv('OSSRH_PASSWORD') })

    if (!isReleaseVersion) {
        it.mavenCentralSnapshotsRepository()
    }
}

publishing {
    publications {
        javaOSSRH(MavenPublication) {
            it.groupId = rootProject.group
            it.artifactId = project.name.equals('framework')
                    ? rootProject.name + '-impl'
                    : (project.name.equals('bom') ? rootProject.name : rootProject.name + '-' + project.name)
            it.version = rootProject.version

            it.pom {
                developers {
                    developer {
                        name.set('Natan Vieira do Nascimento')
                        email.set('natanvnascimento@gmail.com')
                        url.set('https://github.com/DevNatan/')
                    }
                }
            }
        }
    }
}

signing {
    required { isReleaseVersion && gradle.taskGraph.hasTask("uploadArchives") }
    useInMemoryPgpKeys(
            project.findProperty("signing.keyId") ?: System.getenv('OSSRH_SIGNING_KEY'),
            project.findProperty("signing.password") ?: System.getenv('OSSRH_SIGNING_PASSWORD')
    )
    sign publishing.publications.javaOSSRH
}
